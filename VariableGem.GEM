;This is a geometric file for simion of the TOF section of PLASTIC
;Author: Alex Crawshaw
;Date: July 2, 2001
PA_Define(140,210,1,Cyl,y,Electrostatic) ;Defining the PA
;_______________________________

electrode(10)
{
fill
	{
	within{box(60,35,64,47)};mcp
	}
}
electrode(20)
{
fill
	{
	within{box(39,99,79,100)};Outer shell of TOF
	within{polyline(56,99,56,83.5,57,83,59,83,59,99)};Outer most chamfer
	within{polyline(47,99,47,94.5,49,94.5,51,95.5,51,99)}	 ;top outer chamfer
	within{polyline(58,83.5,58,61.5)};grid
	
; -- Aperture that we want to stay the same
;_______________________________
	within{box(2,58,10,66)}; Carbon foil holders:inner
	within{polyline(2,65,6,71,6,65)}
	within{polyline(6,65,10,71,10,65)}
;_______________________________
	within{box(2,88,17,99)}; Carbon foil holders:outer
	within{polyline(2,88,6,84,6,88)}
	within{polyline(6,88,10,84,10,88)}
;_______________________________
	within{polyline(6,70,6,86)};grid
	within{polyline(10,70,10,86)};Carbon Foil
	
; -- end -- 
	within{box(60,62,64,83)};SSD
;_______________________________
	;within{box(60,29,64,30)};inner shell
;_______________________________
	within{box(15,47,17,69.5)};structures under foils
	within{box(10,65,17,69)}
	;within{polyline(10,70.5,12,70.5,14,69.5,10,69.5)}
	;within{polyline(10,79,12,79,14,80,10,80)}
;_______________________________
	within{polyline(16,88,27,88)}
	within{polyline(13,87,9,87)}
	within{polyline(10,85,15,87.5,25,87.5,45,93.5,45,100,47,100,47,92.5,26,86.5,17,86.5,17,85,10,85)};outer ring
	within{polyline(17,47-yvar,57,30,55,31,21, 47-yvar, 21,47.5,21,52,17,52,17,47-yvar)};inner ring

	within{box(34-xvar,52,36-xvar,42-yvar)}; rib on inner cone
	within{polyline(44,math.floor(11/34*(16-yvar)+31), 36-xvar, math.floor((19+xvar)*(16-yvar)/34+31), 44, math.floor((19+xvar)*(16-yvar)/34+31)+7)} ; triangle net to the inner rib


	within{polyline(25,86.5,25,82.5,27,82.5,27,86.5)};first rib on outer cone
	within{polyline(31,88,31,84,33,83,33,88.5)};second rib
	within{polyline(37,89.5,37,85.5,39,84.5,39,90)};third rib
	within{polyline(43,91.5,43,87.5,45,86.5,45,92)};forth rib
;_______________________________
	within{polyline(59,62,57,62,56,61.5,56,54.5,51,54.5,51,53.5,59,53.5)};middle chamfer
	}
}
electrode(30);pulling electrode
{
 fill
	{
	within{polyline(36,56,22,56,22,67,24,68,24,60,28,60,28,65,30,66,30,60,34,60,34,62,36,63,36,56)}; pulling electrode
	}
}
electrode(4);mcp 
{
 fill
	{
	within{polyline(59,47,57,47,57,50,44,50,44,53,46,53,46,51,59,51)};outer ring
	within{polyline(59,35,57,35,57,28.5,56,28.5,56,20,58,20,58,25.5,59,25.5)}
	within{polyline(58,47,58,35)}
	}
}
;outer grounding shell
electrode(50)
{
;	box(1,16,100,207)
	fill
	{
		within
		{
			box(1,0,100,207)
		}
		notin
		{
			box(2,0,99,206)
		}
	}
}





